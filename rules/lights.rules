/**
 * Various rules to turn on/off the lights.
 *  - Turn on the foyer light when garage or front door is open.
 *  - For any light that was turned on programatically, automatically turn off
 *    after 5 minutes.
 *  - Cancel turn-off timer if the light was switched off manually.
 *
 * The timers are not enabled if the system is in vacation mode when the lights
 * are turned on/off randomly to simulate owner presence (thief prevention).
 *
 * Dependencies:
 *   - time-of-day.rules for calculation of day periods.
 *   - presence.rules for VT_In_Vacation setting.
 */

import java.util.HashMap

val LOGGER_NAME = "Lights"
val Object switchLock = new Object()
val Object timerLock = new Object()

/**
 * The period of time in seconds (from the last timestamp a switch was turned
 * off) to ignore the ON command trigged by the motion sensor. This takes care
 * of the scenario when the user manually turns off a light, but that physical
 * spot is covered by a motion sensor, which immediately turns on the light
 * again.
 */
val DELAY_AFTER_LAST_OFF_TIME_IN_MS = 8000 // 8 secs

var HashMap<String, Long> lastOffTimes = newHashMap()

rule "Update light states" 
when
  System started 
then
  FF_Foyer_LightSwitch_MotionSensor.sendCommand(OFF)
end

rule "Update light-on time" 
when
  System started or
  Item VT_Time_Of_Day changed
then
  if (VT_Time_Of_Day.state == "EVENING" 
        || VT_Time_Of_Day.state == "NIGHT"
        || VT_Time_Of_Day.state == "BED" ) {
    VT_Time_LightOn.sendCommand(ON)
  }
  else {
    VT_Time_LightOn.sendCommand(OFF)
  }
end

rule "Turn on light when motion sensor triggered"
when
 Member of gWallSwitchMotionSensor changed to ON
then
  if (ON != VT_Time_LightOn.state ) {
    return
  }

  synchronized(switchLock) {
    val localIdx = triggeringItem.name.lastIndexOf("_")
    val switchName = triggeringItem.name.substring(0, localIdx)

    val switchItem = gWallSwitch.members.findFirst[ t | t.name == switchName ]
    if ( null === switchItem ) {
      return
    }

    if ( ON != switchItem.state ) {
      // If a wall switch was just turned off, ignore the motion sensor event.
      if ( lastOffTimes.containsKey(switchItem.name) ) {
        val long timestamp = lastOffTimes.get(switchItem.name)
        if (now.getMillis() - timestamp <= DELAY_AFTER_LAST_OFF_TIME_IN_MS) {
          return
        }
      }

      // An open area might have multiple lights with a shared motion sensor
      // (e.g. in the security system motion sensor where the motion sensor
      // tends to be in a corner and cover the whole lobby). In this case, we
      // only want to trigger a single light if all lights were off. However,
      // if any light is already on, we still want to renew the timer to keep
      // the light on.
      //
      // Check to see if the motion sensor is allowed to trigger the light.
      val disableAlwaysItemName = triggeringItem.name + "_DisableTriggeringAlways"
      val disableTriggeringAlwaysItem = gMotionSensorDisableTriggeringAlways.members.findFirst[ 
          t | t.name == disableAlwaysItemName 
      ]
      if (null !== disableTriggeringAlwaysItem 
            && ON == disableTriggeringAlwaysItem.state) {
        return
      }

      // Check to see if there is a dependent relationship between lights.
      // I.e. if light B is already on, then don't turn on light A if its
      // motion sensor is triggered.
      val disableIfItemName = triggeringItem.name + "_DisableTriggeringIf"
      val disableTriggeringIfItem = gMotionSensorDisableTriggeringIf.members.findFirst[ 
          t | t.name == disableIfItemName 
      ]
      if (null !== disableTriggeringIfItem
          && null !== disableTriggeringIfItem.state) {
        // see if the other light is on
        val theOtherLight = gWallSwitch.members.findFirst[ 
            t | t.name == disableTriggeringIfItem.state.toString ]
        if ( ON == theOtherLight.state ) {
          return
        }
        else {
          // If it was just turned off, then don't trigger this light yet.
          // This might be the case that the user is getting out of this zone
          if ( lastOffTimes.containsKey(theOtherLight.name) ) {
            val long timestamp = lastOffTimes.get(theOtherLight.name)
            if (now.getMillis() - timestamp <= DELAY_AFTER_LAST_OFF_TIME_IN_MS) {
              return
            }
          } // has OFF timestamp
        } // the other light is of
      }

      switchItem.sendCommand(ON)
    }
    else { // renew timer
      val timerName = switchName + "_Timer"
      val timerItem = gWallSwitchTimer.members.findFirst[ t | t.name == timerName ]
      if ( null !== timerItem ) {
        timerItem.sendCommand(ON)
      }
    }
  }
end

rule "Set a timer to turn off the light after it was programatically turned on"
when
 Member of gWallSwitch changed
then
  synchronized(switchLock) {

    val timerItemName = triggeringItem.name + "_Timer"
    val timerItem = gWallSwitchTimer.members.findFirst[ t | t.name == timerItemName ]

    // some light might stay on until manually turned off
    if ( null === timerItem ) {
      return
    }

    if ( ON == triggeringItem.state ) {
      // When in vacation mode, the lights be turned on/off randomly to
      // simulate presence (thief prevention). The simulation rule has full
      // control of the lights; thus we don't want to enable the timer.
      if ( ON != VT_In_Vacation ) {
        timerItem.sendCommand(ON)
      }
    }
    else {
      if ( ON == timerItem.state ) {
        timerItem.sendCommand(OFF)
      }


      lastOffTimes.put(triggeringItem.name, now.getMillis())
    }
  }
end

rule "Turn off light when timer expires"
when
  Member of gWallSwitchTimer changed to OFF
then
  synchronized(timerLock) {
    val localIdx = triggeringItem.name.lastIndexOf("_")
    val switchName = triggeringItem.name.substring(0, localIdx)

    val target = gWallSwitch.members.findFirst[ t | t.name == switchName ]
    if ( ON == target.state  ) {
      target.sendCommand(OFF)
    }
  }
end
